plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = 'com.magmaguy'
version = '1.6.4-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    //Bstats API
    maven { url = 'https://repo.codemc.org/repository/maven-public' }

    //WorldEdit API
    maven { url = 'https://maven.enginehub.org/repo/' }

    //CloudCommands
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    //EliteMobs
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots' }

    // carm start - Support MMOItems and MythicMobs
    //MMOItems
    maven { url = 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
    //MythicMobs
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    // carm end - Support MMOItems and MythicMobs
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'
    //BStats API
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '2.2.1'
    //WorldGuard API
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard-bukkit', version: '7.0.7'
    //WorldEdit API
    compileOnly group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '7.2.14'
    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    //CloudCommands
    implementation group: 'cloud.commandframework', name: 'cloud-paper', version: '1.5.0'
    //CloudCommands Minecraft Extras module
    implementation group: 'cloud.commandframework', name: 'cloud-minecraft-extras', version: '1.5.0'
    //CloudCommands Adventure Platform module
    implementation("net.kyori:adventure-platform-bukkit:4.0.0")
    //I can't remember what this was for but it's in CloudCommands
    implementation group: 'io.leangen.geantyref', name: 'geantyref', version: '1.3.4'
    //apache commons
    implementation 'org.apache.commons:commons-lang3:3.9'
    //reflections - used for config initialization
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    //EliteMobs
    compileOnly group: 'com.magmaguy', name: 'EliteMobs', version: '8.3.12-SNAPSHOT'

    // carm start - Support MMOItems and MythicMobs
    //MythicMobs
    compileOnly group: 'io.lumine', name: 'Mythic-Dist', version: '5.2.1'
    //MythicLib
    compileOnly group: 'io.lumine', name: 'MythicLib-dist', version: '1.5.2-SNAPSHOT'
    //MMOItems
    compileOnly group: 'net.Indyuce', name: 'MMOItems-API', version: '6.9.2-SNAPSHOT'
    // carm end - Support MMOItems and MythicMobs

}

artifacts { // task 'build' runs generates uberjar
    archives shadowJar
}

jar {
    archiveClassifier.set('min') // we want the Uberjar to be distributed, this is the minified version
}

String packagePath = 'com.magmaguy.shaded'

// Relocating a Package
shadowJar {
    dependencies {
        relocate('org.bstats', packagePath + '.bstats')
        relocate('cloud.commandframework', packagePath + '.cloud')
        relocate('net.kyori', packagePath + '.adventure-platform-bukkit')
        relocate('io.leangen.geantyref', packagePath + '.geantyref')
        relocate('org.reflections', packagePath + '.reflections')
    }
    archiveClassifier.set(null)
    archiveFileName.set(project.name + ".jar")
    destinationDirectory.set(new File("testbed/plugins"))
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

ext {
    resourceTokens = [
            'Version': version
    ]
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

//sonatype repo
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'com.magmaguy'
                name = 'betterstructures'
                description = 'BetterStructures repo'
                url = 'https://example.com/'
                from components.java
                licenses {
                    license {
                        name = 'GPLV3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
            }
        }
    }
}
